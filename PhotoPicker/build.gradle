apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 27
    buildToolsVersion "26.0.2"
    resourcePrefix "__picker_"
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:27.0.2"
    compile "com.android.support:design:27.0.2"
    compile "com.android.support:recyclerview-v7:27.0.2"
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.yalantis:ucrop:2.2.0'
    compile 'com.github.bumptech.glide:glide:4.3.1'
}

// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

