apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"
    resourcePrefix "__picker_"
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:25.1.0"
    provided "com.android.support:design:25.1.0"
    provided "com.android.support:recyclerview-v7:25.1.0"
    provided 'com.nineoldandroids:library:2.4.0'
    compile 'com.yalantis:ucrop:2.2.0'
    provided 'com.github.bumptech.glide:glide:3.7.0'
}

// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

